#version 430

// 112
struct Block
{
    ivec4 texCoords[6]; // 6 * 16 = 96
    int positionAndFaceMask; // 4
};

// 3616
struct Meshlet
{
    vec3 normal; // 16
    ivec4 chunkPosAndBlockCount; // 16
    Block blocks[32]; // 32 * 112 = 3584
};

// 32
struct Vertex
{
    vec3 pos; // 16
    vec2 texCoord; // 8
};

layout(local_size_x = 1) in;

layout(std430, binding = 0) buffer InputMeshlets
{
    Meshlet inMeshlets[];
};

layout(std430, binding = 1) buffer OutputVertices
{
    Vertex outVertices[];
};

layout(std430, binding = 2) buffer OutputIndices
{
    int outIndices[];
};

layout(std430, binding = 3) buffer Counters
{
    uint vertexCounter;
    uint indexCounter;
};

const int FACE_X_POS = 5;
const int FACE_X_NEG = 4;
const int FACE_Y_POS = 3;
const int FACE_Y_NEG = 2;
const int FACE_Z_POS = 1;
const int FACE_Z_NEG = 0;

vec2 decodeTexCoordFromInt(int packedBits)
{
    int a = packedBits & 0xFFFF;
    int b = (packedBits >> 16) & 0xFFFF;
    return vec2(float(a) / 65535.0, float(b) / 65535.0);
}

void emitQuad(vec3 p0, vec3 p1, vec3 p2, vec3 p3, ivec4 tc)
{
    uint vBase = atomicAdd(vertexCounter, 4u);
    uint iBase = atomicAdd(indexCounter, 6u);

    outVertices[vBase + 0].pos = p0;
    outVertices[vBase + 0].texCoord = decodeTexCoordFromInt(tc.x);

    outVertices[vBase + 1].pos = p1;
    outVertices[vBase + 1].texCoord = decodeTexCoordFromInt(tc.y);

    outVertices[vBase + 2].pos = p2;
    outVertices[vBase + 2].texCoord = decodeTexCoordFromInt(tc.z);

    outVertices[vBase + 3].pos = p3;
    outVertices[vBase + 3].texCoord = decodeTexCoordFromInt(tc.w);

    outIndices[iBase + 0] = int(vBase + 0);
    outIndices[iBase + 1] = int(vBase + 1);
    outIndices[iBase + 2] = int(vBase + 2);

    outIndices[iBase + 3] = int(vBase + 2);
    outIndices[iBase + 4] = int(vBase + 3);
    outIndices[iBase + 5] = int(vBase + 0);
}

void main()
{
    uint meshletID = gl_GlobalInvocationID.x;
    Meshlet m = inMeshlets[meshletID];

    int blockCount = clamp(m.chunkPosAndBlockCount.w, 0, 32);

    for (int b = 0; b < blockCount; ++b)
    {
        Block block = m.blocks[b];
        int posFace = block.positionAndFaceMask;

        int faceMask = posFace & 0x3F;
        int x = (posFace >> 6)  & 0xF;
        int y = (posFace >> 10) & 0xF;
        int z = (posFace >> 14) & 0xF;

        vec3 base = vec3(x, y, z);

        if ((faceMask & (1 << FACE_X_POS)) != 0)
        {
            emitQuad(
                base + vec3(1, 1, 1), // yz<1,1>
                base + vec3(1, 0, 1), // yz<0,1>
                base + vec3(1, 0, 0), // yz<0,0>
                base + vec3(1, 1, 0), // yz<1,0>
                block.texCoords[FACE_X_POS]
            );
        }

        if ((faceMask & (1 << FACE_X_NEG)) != 0)
        {
            emitQuad(
                base + vec3(0, 1, 0), // yz<1,0>
                base + vec3(0, 0, 0), // yz<0,0>
                base + vec3(0, 0, 1), // yz<0,1>
                base + vec3(0, 1, 1), // yz<1,1>
                block.texCoords[FACE_X_NEG]
            );
        }

        if ((faceMask & (1 << FACE_Y_POS)) != 0)
        {
            emitQuad(
                base + vec3(0, 1, 0), // xz<0,0>
                base + vec3(0, 1, 1), // xz<0,1>
                base + vec3(1, 1, 1), // xz<1,1>
                base + vec3(1, 1, 0), // xz<1,0>
                block.texCoords[FACE_Y_POS]
            );
        }

        if ((faceMask & (1 << FACE_Y_NEG)) != 0)
        {
            emitQuad(
                base + vec3(0, 0, 1), // xz<0,1>
                base + vec3(0, 0, 0), // xz<0,0>
                base + vec3(1, 0, 0), // xz<1,0>
                base + vec3(1, 0, 1), // xz<1,1>
                block.texCoords[FACE_Y_NEG]
            );
        }

        if ((faceMask & (1 << FACE_Z_POS)) != 0)
        {
            emitQuad(
                base + vec3(0, 1, 1), // xy<0,1>
                base + vec3(0, 0, 1), // xy<0,0>
                base + vec3(1, 0, 1), // xy<1,0>
                base + vec3(1, 1, 1), // xy<1,1>
                block.texCoords[FACE_Z_POS]
            );
        }

        if ((faceMask & (1 << FACE_Z_NEG)) != 0)
        {
            emitQuad(
                base + vec3(1, 1, 0), // xy<1,1>
                base + vec3(1, 0, 0), // xy<1,0>
                base + vec3(0, 0, 0), // xy<0,0>
                base + vec3(0, 1, 0), // xy<0,1>
                block.texCoords[FACE_Z_NEG]
            );
        }
    }
}
